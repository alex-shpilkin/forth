vocabulary shim   also shim definitions
S" shim.ans" included

vocabulary meta   also meta definitions
S" image.fth" included   ( ub@ b!   memory range empty ihex )
S" here.fth"  included   ( here allot b, )
S" assem.fth" included   ( w! w, etc.    )
S" proto.fth" included   ( 'read etc.    )

hex

100 memory
  8000 0100 range

5240 ( UART2 )
  dup     constant SR     dup 1 + constant DR
  dup 2 + constant BRR1   dup 3 + constant BRR2
    ( 4 +          CR1 )  dup 5 + constant CR2
drop

0vectors                                ( vector table  )

8080 to here                            ( start of code )
: entry ( a -- )   here <> ?abort" entry? " ;

here constant recvw
  forward cal,   ( FALLTHROUGH )        ( rx high low   )
here constant recvb   resolve
  here & SR ) 5 tjf,                    ( rx wait       )
  DR ) lda,   x rla,                    ( rx read       )
  SR ) 5 res,   ret,                    ( rx release    )

'read entry
  recvw & cal,   x) lda,                ( rx address    )
  here & SR ) 7 tjf,   DR ) sta,        ( tx byte       )
  forward jmp,                          ( loop          )

'write entry
  recvb & cal,   recvw & cal,           ( rx byte addr  )
  x) sta,   forward jmp,                ( store & loop  )

here 0 !vector                          (  ENTRY POINT  )

50C6 ) 0 # mov,                         ( 16MHz HSI osc )
BRR2 ) 0A # mov,   BRR1 ) 08 # mov,     ( 115200 baud   )
CR2  ) 0C # mov,                        ( rx/tx enable  )

'loop entry ( write> ) resolve ( read> ) resolve

recvw & cal,   x) jmp,                  ( rx addr & jmp )

ihex bye
