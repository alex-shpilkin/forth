vocabulary shim   also shim definitions
S" shim.ans" included

vocabulary meta   also meta definitions
S" image.fth" included   ( ub@ b!   memory range empty ihex )
S" where.fth" included   ( where b,      )
S" assem.fth" included   ( w! w, etc.    )
S" proto.fth" included   ( 'read etc.    )

hex

100 memory
  8000 0100 range

5240 ( UART2 )
  dup     constant SR     dup 1 + constant DR
  dup 2 + constant BRR1   dup 3 + constant BRR2
    ( 4 +          CR1 )  dup 5 + constant CR2
drop

0vectors                                ( vector table  )

8080 where !                            ( start of code )
: entry ( a -- )   where @ <> ?abort" entry? " ;

where @ constant recvw
  forward cal,   ( FALLTHROUGH )        ( rx high low   )
where @ constant recvb   resolve
  where @ & SR ) 5 tjf,   DR ) lda,     ( rx byte in A  )
  SR ) 5 res,                           ( clear buffer  )
  ( FALLTHROUGH )                       ( echo, shift   )
where @ constant sendb
  where @ & SR ) 7 tjf,   DR ) sta,     ( tx byte in A  )
  x rla,   ret,                         ( shift into X  )

'read entry
  recvw & cal,   x) lda,                ( rx address    )
  sendb & cal,   forward jmp,           ( tx byte, loop )

'write entry
  recvb & cal,   recvw & cal,           ( rx byte addr  )
  x) sta,   forward jmp,                ( store & loop  )

where @ 0 !vector                       (  ENTRY POINT  )

50C6 ) 0 # mov,                         ( 16MHz HSI osc )
BRR2 ) 0A # mov,   BRR1 ) 08 # mov,     ( 115200 baud   )
CR2  ) 0C # mov,                        ( rx/tx enable  )

'loop entry ( write> ) resolve ( read> ) resolve

recvw & cal,   x) jmp,                  ( rx addr & jmp )

ihex bye
